plugins {
    id 'kotlin'
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm'
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.7.22'
}

sourceCompatibility = "11"
targetCompatibility = "11"
version = rootProject.version

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

tasks.register('installToPrefix', Copy) {
    dependsOn(installShadowDist)
    from("build/install/bank-shadow") {
        include("**/libeufin-bank")
        include("**/*.jar")
    }
    /**
     * Reads from command line -Pkey=value options,
     * with a default (/tmp) if the key is not found.
     *
     * project.findProperty('prefix') ?: '/tmp'
     */
    into "${project.findProperty('prefix') ?: '/tmp'}"
}
// apply plugin: 'kotlinx-serialization'

sourceSets {
    main.java.srcDirs = ['src/main/kotlin']
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1-native-mt'
    implementation 'ch.qos.logback:logback-classic:1.4.5'
    implementation project(":util")


    // XML:
    implementation "javax.xml.bind:jaxb-api:2.3.0"
    implementation "org.glassfish.jaxb:jaxb-runtime:2.3.1"

    implementation 'org.postgresql:postgresql:42.2.27'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.21'
    implementation('com.github.ajalt:clikt:2.8.0')

    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-call-logging:$ktor_version"
    implementation "io.ktor:ktor-server-cors:$ktor_version"
    implementation "io.ktor:ktor-server-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-server-status-pages:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "io.ktor:ktor-client-auth:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-server-test-host:$ktor_version"
    implementation "io.ktor:ktor-auth:$ktor_auth_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1"
    implementation("io.ktor:ktor-serialization-kotlinx-json:$ktor_version")
    implementation "io.ktor:ktor-server-request-validation:$ktor_version"

    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.5.21'
    testImplementation 'org.jetbrains.kotlin:kotlin-test:1.5.21'
    testImplementation group: "junit", name: "junit", version: '4.13.2'

    testImplementation project(":util")

    // UNIX domain sockets support (used to connect to PostgreSQL)
    implementation 'com.kohlschutter.junixsocket:junixsocket-core:2.6.2'
}

application {
    mainClassName = "tech.libeufin.bank.MainKt"
    applicationName = "libeufin-bank"
    applicationDefaultJvmArgs = ['-Djava.net.preferIPv6Addresses=true']
}

jar {
    manifest {
        attributes "Main-Class": "tech.libeufin.bank.MainKt"
    }
}
