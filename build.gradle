// This file is in the public domain.

import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id("org.jetbrains.kotlin.jvm") version "1.9.20"
    id("idea")
    id("java-library")
    id("maven-publish")
}

group = "tech.libeufin"
version = "0.9.3"

if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)){
    throw new GradleException(
        "This build must be run with java 17 " +
        "or later (your version is java ${JavaVersion.current()})")
}

allprojects {
    ext {
        set("kotlin_version", "1.9.20")
        set("ktor_version", "2.3.6")
        set("netty_version", "4.1.100.Final")
        set("clikt_version", "4.2.1")
        set("json_version", "1.6.1")
        set("coroutines_version", "1.7.3")
        set("postgres_version", "42.7.0")
    }

    repositories {
        mavenCentral()
    }
}

idea {
    module {
        excludeDirs += file("frontend")
    }
}

task versionFile() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    def gitHash = stdout.toString().trim()
    def version = getRootProject().version
    new File("${projectDir}/util/src/main/resources", "version.txt").text = "v$version-git-$gitHash"
}

// See: https://stackoverflow.com/questions/24936781/gradle-plugin-project-version-number
task libeufinVersion {
    doLast {
        println project.version
    }
}

classes {
    dependsOn versionFile
}
